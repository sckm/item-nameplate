apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation project(':item-nameplate-view-holder')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'

    kapt "com.google.auto.service:auto-service:1.0-rc4"
    implementation "com.willowtreeapps.hyperion:hyperion-core:0.9.27"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties.getProperty('bintrayUser') ?: ""
    key = properties.getProperty('bintrayKey') ?: ""

    group = 'com.github.sckm'
    version = ITEM_NAMEPLATE_VERSION
    configurations = ['archives']

    pkg {
        repo = 'maven'
        userOrg = 'scache'
        name = 'hyperion-item-nameplate-view-holder'
        vcsUrl = 'https://github.com/sckm/item-nameplate.git'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/sckm/item-nameplate'
        version {
            name = ITEM_NAMEPLATE_VERSION
            desc = 'Android library for showing list item name'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'hyperion-item-nameplate-view-holder'
                url 'https://github.com/sckm/item-nameplate'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'scache'
                        name 'scache'
                        email 'scal.che+dev@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/sckm'
                    developerConnection 'https://github.com/sckm'
                    url 'https://github.com/sckm'
                }
            }
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/documentation"
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives dokkaJar
    archives sourcesJar
}
